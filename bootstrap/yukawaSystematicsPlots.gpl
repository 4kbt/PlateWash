set macro

#Flag for plotting gravity alone, instead of all systematics.
gravityFlag = "gravityOnly"

fn = system("echo $GNUPLOTME");
outfile = fn .".eps"

if ( exists( gravityFlag) ) {
	outfile = fn .".grav.eps"
}

load HOMEDIR.'/globalConfig/columnNames.m'

cookFile = HOMEDIR.'/thesis/plots/ISL/cookData.dat'
kapFile  = HOMEDIR.'/thesis/plots/ISL/kapner95limit.dat'

set term postscript enhanced solid color size 9,15
set output outfile 

set logsc x
set logsc y

chiCut = 2 
#chiMin  = 0.75 #e-10
chiMin  = 0.75e-10

iterMin =  15 

cutFunc = "( (column(chiCol) < chiCut*column(NDF)) & (column(chiCol) > chiMin*column(NDF)) & (column(iterCol) > iterMin ))"

set title 'bootstrapped best fits, with systematics'

NumSystematics = numSystematics+1 #treats science as a systematic

NDF = 1+2*NumSystematics+5+1+2*NumSystematics+2
chiCol = 1+2*NumSystematics+1
iterCol = 1 + 2 * NumSystematics + 3

alMax = 1e8
alMin = 1e-6


ptsz = 0.7
set key noauto
set format x ""
set ylabel 'alpha'
set format y "10^{%T}"
set multiplot layout 4,1

#pl [0.009:102][alMin:alMax]\

NumToPlot = NumSystematics
if ( exists(gravityFlag) ) {
	NumToPlot = 1
}

pl [:][alMin:]\
	kapFile w l lt -1  tit 'Kapner et al.',\
	cookFile w l lt 0 tit "Cook thesis",\
	for [ctr=NumToPlot:1:-1] \
	fn index 0 u ( @cutFunc ? column(2*ctr) * XLUnits : 1/0):( column(2*ctr+1)) w p lc ctr pt 7 ps ptsz,\
	fn index 0 u ( @cutFunc ? column(2*ctr+2*NumSystematics+5) * XLUnits : 1/0):( column(2*ctr+1+2*NumSystematics+5 )) w p lc -1 pt 1 ps 2,\
	for [ctr=NumToPlot:1:-1] \
	fn index 1 u (column(ctr)):(column(ctr+NumSystematics)) w p lc ctr pt 1 ps ptsz*3
	

unset title

set yrange [:] reverse
set key below noauto
set format x "10^{%T}"
set ylabel 'negative alpha'
set xlabel 'lambda (m)'
#pl [0.009:102][alMin:alMax]\

pl [:][alMin:]\
	kapFile  w l lt -1  ,\
	cookFile w l lt 0   ,\
	for [ctr=NumToPlot:1:-1] \
	fn index 0 u (@cutFunc ? column(2*ctr) * XLUnits: 1/0):( -column(2*ctr+1)) w p lc ctr pt 7 ps ptsz\
			title columnhead(1+2*ctr),\
	fn index 0 u ( @cutFunc ? column(2*ctr+2*NumSystematics+5) * XLUnits: 1/0):( -column(2*ctr+1+2*NumSystematics+5 )) w p lc -1 pt 1 ps 2,\
	for [ctr=NumToPlot:1:-1] \
	fn index 1 u (column(ctr)):(-column(ctr+NumSystematics)) w p lc ctr pt 1 ps ptsz*3
	

set format x "% g"
unset logsc x
#unset logsc y 
set yrange [:] noreverse
set ylabel 'slope'
set xlabel 'fit #'
pl \
	fn index 0 u ( @cutFunc ?  $1 * XSUnits / 1e-15 : 1/0) w p pt 7 ps ptsz lc 1 tit 'positive',\
	fn index 0 u ( @cutFunc ? -$1 * XSUnits / 1e-15: 1/0) w p pt 7 ps ptsz lc 3 tit 'negative',\
	fn index 1 u (column(NumSystematics*2+1) * XSUnits / 1e-15) w p pt 1 ps ptsz*3 lc 1 lw 2 tit 'injected signal, if positive',\
	fn index 1 u (-column(NumSystematics*2+1) *XSUnits / 1e-15) w p pt 1 ps ptsz*3 lc 3 lw 2 tit 'injected signal, if negative'
	
	
set logsc y 
set ylabel 'chi squared/NDF'
set format y "% g"
pl \
	fn index 0 u (column(iterCol) > iterMin ? column(chiCol)/column(NDF) : 1/0) w p pt 7 ps ptsz lc 3,\
	fn index 0 u (chiMin) w l lt 1 lw 2 tit 'min',\
	fn index 0 u (chiCut) w l lt 3 lw 2 tit 'max'

unset multiplot
unset xlabel

