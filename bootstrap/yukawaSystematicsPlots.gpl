set macro

#Flag for plotting gravity alone, instead of all systematics.
gravityFlag = "gravityOnly"

fn = system("echo $GNUPLOTME");
outfile = fn .".eps"

cn = 'output/confidenceInterval.dat'

if ( exists( gravityFlag) ) {
	outfile = fn .".grav.eps"
}

load HOMEDIR.'/globalConfig/columnNames.m'

cookFile = HOMEDIR.'/thesis/plots/ISL/cookData.dat'
kapFile  = HOMEDIR.'/thesis/plots/ISL/kapner95limit.dat'

set term postscript enhanced solid color size 9,15
set output outfile 

set logsc x
set logsc y

chiCut = 2 
#chiMin  = 0.75 #e-10
chiMin  = 0.75e-10

iterMin =  5 

cutFunc = "( (column(chiCol) < chiCut*column(NDF)) & (column(chiCol) > chiMin*column(NDF)) & (column(iterCol) > iterMin ))"

set title 'bootstrapped best fits, with systematics'

NumSystematics = numSystematics+1 #treats science as a systematic

NDF = 1+2*NumSystematics+5+1+2*NumSystematics+2
chiCol = 1+2*NumSystematics+1
iterCol = 1 + 2 * NumSystematics + 3

alMax = 1e8
alMin = 1e-5


ptsz = 0.7
set key noauto
set format x ""
set ylabel 'alpha'
set format y "10^{%T}"
set multiplot layout 4,1

#pl [0.009:102][alMin:alMax]\

NumToPlot = NumSystematics
if ( exists(gravityFlag) ) {
	NumToPlot = 1
}

pl [:][alMin:]\
	kapFile w l lt -1  tit 'Kapner et al.',\
	cookFile w l lt 0 tit "Cook thesis",\
	for [ctr=NumToPlot:1:-1] \
	fn index 0 u ( @cutFunc ? column(2*ctr) * XLUnits : 1/0):( column(2*ctr+1)) w p lc ctr pt 7 ps ptsz,\
	for [ctr=NumToPlot:1:-1] \
	fn index 1 u (column(ctr) * XLUnits):(column(ctr+NumSystematics)) w p lc ctr pt 1 ps ptsz*3,\
	cn u ($3*XLUnits):($5+2.0*$6):(($4-$3)*XLUnits):(0) w vectors nohead lt 3 lw 2 tit 'Gravity 95% inclusion',\
	cn u ($3*XLUnits):($5-2.0*$6):(($4-$3)*XLUnits):(0) w vectors nohead lt 3 lw 2
	

unset title

set yrange [:] reverse
set key below noauto
set format x "10^{%T}"
set ylabel 'negative alpha'
set xlabel 'lambda (m)'
#pl [0.009:102][alMin:alMax]\

pl [:][alMin:]\
	kapFile  w l lt -1  ,\
	cookFile w l lt 0   ,\
	for [ctr=NumToPlot:1:-1] \
	fn index 0 u (@cutFunc ? column(2*ctr) * XLUnits: 1/0):( -column(2*ctr+1)) w p lc ctr pt 7 ps ptsz\
			title columnhead(1+2*ctr),\
	for [ctr=NumToPlot:1:-1] \
	fn index 1 u (column(ctr) * XLUnits):(-column(ctr+NumSystematics)) w p lc ctr pt 1 ps ptsz*3,\
	cn u ($3*XLUnits):(-$5+2.0*$6):(($4-$3)*XLUnits):(0) w vectors nohead lt 3 lw 2,\
	cn u ($3*XLUnits):(-$5-2.0*$6):(($4-$3)*XLUnits):(0) w vectors nohead lt 3 lw 2
	

set format x "% g"
unset logsc x
#unset logsc y 
set yrange [:] noreverse
set ylabel 'slope (fN/mm)'
set xlabel 'fit #'

slopeCal = XSUnits / 1e-15 * 1e-3;


#stats doesn't work with logsc
unset logsc y
#the multiplication transcends trouble with the columnheader strings
stats fn index 0  u (1*$1) name "RECORDS"
recWidth = RECORDS_records/2;
set logsc y

pl \
	fn index 0 u ( @cutFunc ?  $1 * slopeCal : 1/0) w p pt 7 ps ptsz lc 1 tit 'positive',\
	fn index 0 u ( @cutFunc ? -$1 * slopeCal : 1/0) w p pt 7 ps ptsz lc 3 tit 'negative',\
	fn index 1 u (recWidth):( column(NumSystematics*2+1)  * slopeCal ):(recWidth):(0) w xyerrorlines pt 1 ps 0 lc 1 lw 2 tit 'injected signal, if positive',\
	fn index 1 u (recWidth):(-column(NumSystematics*2+1)  * slopeCal ):(recWidth):(0) w xyerrorlines pt 1 ps 0 lc 3 lw 2 tit 'injected signal, if negative'
	
	
set logsc y 
set ylabel 'chi squared/NDF'
pl \
	fn index 0 u (column(chiCol)/column(NDF)) w p pt 2 ps ptsz lc -1 tit 'all data',\
	fn index 0 u (column(iterCol) > iterMin ? column(chiCol)/column(NDF) : 1/0) w p pt 1 ps ptsz lc 1 tit 'after fit-iteration cut',\
	fn index 0 u ( @cutFunc ? column(chiCol)/column(NDF) : 1/0) w p pt 7 ps ptsz lc 3 tit 'after all cuts',\
	fn index 0 u (chiMin) w l lt 1 lw 2 tit 'min',\
	fn index 0 u (chiCut) w l lt 3 lw 2 tit 'max'

unset multiplot
unset xlabel

