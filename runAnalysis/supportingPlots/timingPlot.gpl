set term postscript enhanced solid color
set output 'timingPlot.eps'

pwt = '../results/pwTimeInfo.dat'
pst = '../results/psTimeInfo.dat'
pwu = '../results/unCorrectedpwTimeInfo.dat'
psu = '../results/unCorrectedpsTimeInfo.dat'

set multiplot layout 2,1

set xlabel 'run duration (d)'
set ylabel 'NTP minus local time (s)'
set key below Left 

pl \
	pwt u (($3-$2)/86400):($4-$3) w p pt 7 ps 2 lc 3 tit 'Torsion Balance Computer',\
	pwu u (($3-$2)/86400):($4-$3) w p pt 6 ps 2 lc 5 tit 'Torsion Balance Computer - uncorrected',\
	pst u (($3-$2)/86400):($4-$3) w p pt 5 ps 2 lc 3 tit 'Attractor Computer',\
	psu u (($3-$2)/86400):($4-$3) w p pt 4 ps 2 lc 5 tit 'Attractor Computer - uncorrected'

set ylabel 'DAQ rate difference from 0.8s (s/d)'

pl  \
	pwt u (($3-$2)/86400):(($4-$2)/$5*86400/0.8-86400) w p pt 7 ps 2 lc 5 tit 'Pendulum Sample Rate, NTP',\
	pwt u (($3-$2)/86400):(($3-$2)/$5*86400/0.8-86400) w p pt 7 ps 2 lc 3 tit 'Pendulum Sample Rate',\
	pst u (($3-$2)/86400):(($4-$2)/$5*86400/0.8-86400) w p pt 4 ps 2 lc 5 tit 'Attractor Sample Rate, NTP',\
	pst u (($3-$2)/86400):(($3-$2)/$5*86400/0.8-86400) w p pt 4 ps 2 lc 3 tit 'Attractor Sample Rate'


unset multiplot
