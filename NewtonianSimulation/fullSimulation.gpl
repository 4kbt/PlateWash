set term postscript enhanced solid color eps
set output 'plots/fullSimulation.eps'

set key top left

set ylabel 'torque (fN-m)'

Gaps      = 'SimulationOutput/jan13ScrewGaps.dat'
Spindles  = 'SimulationOutput/jan13Spindles.dat'
Attractor = 'SimulationOutput/jan13Attractor.dat'
nonAttractor = 'SimulationOutput/nonAttractor.dat'

gaps(x)      = mgaps      + sgaps      * x
spindles(x)  = mspindles  + sspindles  * x
attractor(x) = mattractor + sattractor * x
nonattractor(x) = mnonattractor+snonattractor*x

fit gaps(x)      Gaps      u 5:13 via mgaps, sgaps
fit spindles(x)  Spindles  u 5:13 via mspindles, sspindles
fit attractor(x) Attractor u 5:13 via mattractor, sattractor
fit nonattractor(x) nonAttractor u 5:13 via mnonattractor, snonattractor


pendOff = system( HOMEDIR."bin/texSigToNum.sh ".HOMEDIR."extracted/pendulumBodyThickness.tex")/2.0*1e6

set multiplot layout 2,1
set key off
set format x ""
pl [0:]\
	Gaps      u ($5*1e6-pendOff):(($13 )*1e15) w l tit 'Screw Gaps',\
	Spindles  u ($5*1e6-pendOff):(($13 )*1e15) w l tit 'Spindles',\
	Attractor u ($5*1e6-pendOff):(($13 )*1e15) w l  tit 'Attractor',\
	nonAttractor u ($5*1e6-pendOff):(($13 )*1e15) w l  tit 'All non-Attractor components'


set title 'non-linear residuals'
set xlabel 'pendulum - attractor face separation ({/Symbol m}m)'
set key below
set format x "%g"

pl [0:]\
	Gaps      u ($5*1e6-pendOff):(($13 - gaps($5)     )*1e15) w l tit 'Screw Gaps',\
	Spindles  u ($5*1e6-pendOff):(($13 - spindles($5) )*1e15) w l tit 'Spindles',\
	Attractor u ($5*1e6-pendOff):(($13 - attractor($5))*1e15) w l  tit 'Attractor',\
	nonAttractor u ($5*1e6-pendOff):(($13 - nonattractor($5))*1e15) w l  tit 'All non-Attractor components'

unset multiplot
