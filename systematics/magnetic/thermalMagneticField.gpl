set term postscript enhanced solid color eps size 4,4 
set output 'thermalMagneticField.eps'

#Conductivities
sigTa = 1.0/131e-9
sigAl = 1.0/28.2e-9

#Thicknesses
tTa = 0.0254*30e-3
tAl = 2.6e-3

#Constants
k_B =  1.38065050000000e-23
mu0 =  4.0*pi*1.0e-7

set format x "10^{%T}"
set format y "10^{%T}"
set samples 1000

PropTa = mu0 / 2.0 * sqrt( sigTa * k_B*298.0 * tTa / 2.0 / pi)
PropAl = mu0 / 2.0 * sqrt( sigAl * k_B*298.0 * tAl / 2.0 / pi)

fcAl(z) = 1.0/( 4 * mu0 * sigAl * tAl * z)
fcTa(z) = 1.0/( 4 * mu0 * sigTa * tTa * z)

B(z)   = sqrt( ( PropTa / z )**2 + ( PropAl / (z+tTa) )**2 )
BAl(z) = sqrt(                     ( PropAl / (z+tTa) )**2 )
BTa(z) = sqrt( ( PropTa / z )**2                           )

set logsc
set key noauto
set multiplot layout 2,2 

#set title  'pseudo-DC magnetic field noise'
set xlabel 'distance from attractor face (m)'
#set ylabel 'Low frequency Magnetic Field Noise (T/{/Symbol @{\140\140}\326}Hz)'
#
#pl [1e-6:10e-3] \
#	BAl(x) tit 'Aluminum Backer',\
#	BTa(x) tit 'Tantalum sheet',\
#	B(x) tit 'Combined' 


#pause -1

#Frequency dependence

freqDepAl(f,z) = 1.0/sqrt(1 + ( 2.0* f / pi / fcAl(z) )**2 )
freqDepTa(f,z) = 1.0/sqrt(1 + ( 2.0* f / pi / fcTa(z) )**2 )

BFTa(f,z) = sqrt( ( PropTa / z * freqDepTa(f,z) )**2 )
BF(f,z) =   sqrt( ( PropTa / z * freqDepTa(f,z) )**2 + ( PropAl/(z+tTa) * freqDepAl(f,z+tTa) )**2 )
BFAl(f,z) = sqrt(                                      ( PropAl/(z+tTa) * freqDepAl(f,z+tTa) )**2 )

set ylabel 'Magnetic Field Noise (T/{/Symbol @{\140\140}\326}Hz)'
set title  'Tantalum attractor'

pl [1e-7:100][1e-18:1e-6] \
	BFTa(0   ,x) ,\
	BFTa(0.1 ,x) ,\
	BFTa(1   ,x) ,\
	BFTa(10  ,x) ,\
	BFTa(100 ,x) ,\
	BFTa(1e3 ,x) ,\
	BFTa(1e4 ,x) ,\
	BFTa(1e5 ,x) ,\
	BFTa(1e6 ,x) ,\
	BFTa(1e7 ,x) 

#pause -1 

set title 'Aluminum backer'

pl [1e-7:1][1e-18:1e-6] \
	BFAl(0   ,x) ,\
	BFAl(0.1 ,x) ,\
	BFAl(1   ,x) ,\
	BFAl(10  ,x) ,\
	BFAl(100 ,x) ,\
	BFAl(1e3 ,x) ,\
	BFAl(1e4 ,x) ,\
	BFAl(1e5 ,x) ,\
	BFAl(1e6 ,x) ,\
	BFAl(1e7 ,x) 

#pause -1 
set key at screen 0.8,0.35 
set title 'combined Ta attractor and Al backer'

pl [1e-7:1][1e-18:1e-6] \
	BF(0   ,x) tit '"DC"',\
	BF(0.1 ,x) tit '0.1 Hz',\
	BF(1   ,x) tit '1 Hz',\
	BF(10  ,x) tit '10 Hz',\
	BF(100 ,x) tit '100 Hz',\
	BF(1e3 ,x) tit '1 kHz',\
	BF(1e4 ,x) tit '10 kHz',\
	BF(1e5 ,x) tit '100 kHz',\
	BF(1e6 ,x) tit '1 MHz',\
	BF(1e7 ,x) tit '10 MHz'

unset multiplot
