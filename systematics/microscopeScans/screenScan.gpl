set term postscript enhanced solid color eps 
set output 'screenScan.eps'

set palette model RGB defined (-0.02 "red" , -0.01 "yellow", 0.01 "blue")

set size ratio 1

set key noauto 

set cbrange [-5:5]

set title 'foil scan'
set xlabel 'millimeters'
set ylabel 'millimeters'
set cblabel 'surface height ({/Symbol m}m)'

R2 = 40**2 

f(x,y) = ((x**2+y**2) < R2) ?  a*x+b*y+c : 1/0

fn = 'charlieScreen.dat'

fit [-25:25][-25:25] f(x,y) fn u 1:2:3:(1) via a,b,c

plot [-40:40][-40:40][-20:10] fn u 1:2:\
	( ( ( ($2)**2 + ($1)**2 ) < R2 ) ? (1000.0 * ( $3-f($1,$2) )) : 1/0) \
		w p pt 7 ps 0.5 palette


